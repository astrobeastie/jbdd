group 'de.tum.in'
version '0.5.0'

apply plugin: 'java'

apply plugin: 'pmd'
apply plugin: 'checkstyle'

apply plugin: 'idea'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def defaultEncoding = 'UTF-8'

tasks.withType(JavaCompile) { options.encoding = defaultEncoding }

tasks.withType(Javadoc) { options.encoding = defaultEncoding }

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.immutables:value:2.7.1:annotations'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile 'com.google.guava:guava:28.0-jre'

    annotationProcessor 'org.immutables:value:2.7.1'
}

apply from: 'gradle/analysis.gradle'

// Deployment - run with -Prelease clean uploadArchives closeAndReleaseRepository
if (project.hasProperty('release')) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    // apply plugin: 'io.codearte.nexus-staging'
    apply from: 'gradle/publish.gradle'
}

// If a user file is present, include it.
if (file('../user.gradle').exists()) {
    apply from: '../user.gradle'
}

apply from: 'gradle/idea.gradle'